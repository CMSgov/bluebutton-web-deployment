def aws_creds = 'cbj-deploy'
def private_key = ''
def vault_pass = 'vault-pass'
def backend_config = ''
def terraform_vars = ''
def release_version = ''
def ami_id = ''

pipeline {
  agent {
    node {
      label ''
      customWorkspace 'blue-button-deploy-ami'
    }
  }

  parameters {
    string(
      defaultValue: "*/master",
      description: 'The branch of the deployment repo to use for the build.',
      name: 'DEPLOY_BRANCH'
    )
    string(
      defaultValue: "",
      description: 'The Blue Button AMI id that will be used for the deployment.',
      name: 'AMI_ID'
    )
    string(
      defaultValue: "m3.medium",
      description: 'The class/size of the ec2 instance to launch.',
      name: 'INSTANCE_CLASS'
    )
    choice(
      choices: 'dev\ntest\nimpl\nprod',
      description: 'The environment to deploy to. Required.',
      name: 'ENV'
    )
    choice(
      choices: 'no\nyes',
      description: 'Should we run database migrations on deploy?',
      name: 'MIGRATE'
    )
    choice(
      choices: 'yes\nno',
      description: 'Should we run collectstatic on deploy?',
      name: 'COLLECT_STATIC'
    )
    string(
      defaultValue: "",
      description: 'The number of Terraform to change actions expected. This value will vary based on the number of CloudWatch alarms enabled.',
      name: 'TF_EXPECT_CHANGES'
    )
  }

  stages {
    stage('Ensure ENV') {
      steps {
        script {
          if (params.ENV == "") {
            sh "exit 1"
          }
        }
      }
    }

    stage('Look up AMI ID (dev and test)') {
      steps {
        script {
          withAwsCli(credentialsId: aws_creds, defaultRegion: 'us-east-1') {
            ami_id = sh(
              script: "aws ec2 describe-images --filter 'Name=name,Values=bb-${params.ENV}-*' --query 'sort_by(Images,&CreationDate)[-1].ImageId' --output=text",
              returnStdout: true
            ).trim()
          }
        }
      }
      when {
        expression {
          (params.ENV == "dev" || params.ENV == "test") && params.AMI_ID == ""
        }
      }
    }

    stage('Ensure AMI ID') {
      steps {
        script {
          if (ami_id == "") {
            if (params.AMI_ID != "") {
              ami_id = "${params.AMI_ID}"
            } else {
              sh "exit 1"
            }
          }
        }
      }
    }

    stage('Clear the working dir') {
      steps {
        dir('code') {
          deleteDir()
        }
      }
    }

    stage('Notify HipChat') {
      steps {
        withCredentials([
          string(credentialsId: 'hipchat-room', variable: 'room'),
          string(credentialsId: 'hipchat-server', variable: 'server'),
          string(credentialsId: 'hipchat-token', variable: 'token')
        ]) {
          hipchatSend(
            color: 'GRAY',
            notify: true,
            message: "STARTED: ${env.JOB_NAME} [${params.ENV}]",
            room: room,
            sendAs: '',
            server: server,
            token: token,
            v2enabled: true
          )
        }
      }
    }

    stage('Checkout') {
      steps {
        checkout([
          $class: 'GitSCM',
          branches: [[
            name: "${params.DEPLOY_BRANCH}"
          ]],
          doGenerateSubmoduleConfigurations: false,
          extensions: [[
            $class: 'RelativeTargetDirectory',
            relativeTargetDir: 'code'
          ]],
          userRemoteConfigs: [[
            url: 'https://github.com/CMSgov/bluebutton-web-deployment.git'
          ]]
        ])
        checkout([
          $class: 'GitSCM',
          branches: [[
            name: "*/master"
          ]],
          doGenerateSubmoduleConfigurations: false,
          extensions: [[
            $class: 'RelativeTargetDirectory',
            relativeTargetDir: 'tfenv'
          ]],
          userRemoteConfigs: [[
            url: 'https://github.com/tfutils/tfenv.git']]])
      }
    }

    stage('Install requirements') {
      steps {
        dir('code') {
          script {
            sh """
              virtualenv -ppython2.7 venv
              . venv/bin/activate

              pip install --upgrade pip
              pip install --upgrade cffi

              pip install ansible==2.4.2.0
              pip install boto
            """
          }
        }
      }
    }

    stage('Install Terraform Version') {
      steps {
        dir('tfenv') {
          script {
            sh """
              bin/tfenv install
              terraform --version
            """
          }
        }
      }
    }

    stage('Set private key file') {
      steps {
        script {
          private_key = "${params.ENV}-key"
        }
      }
    }

    stage('Determine terraform config files') {
      steps {
        script {
          backend_config = "bb-backend-${params.ENV}"
          terraform_vars = "bb-tf-${params.ENV}"
        }
      }
    }

    stage('Sanity check terraform plan') {
      steps {
        script {
          dir('code') {
            withAwsCli(credentialsId: aws_creds, defaultRegion: 'us-east-1') {
              withCredentials([
                file(credentialsId: backend_config, variable: 'bc'),
                file(credentialsId: terraform_vars, variable: 'tv')
              ]) {
                sh """
                  cd terraform/${params.ENV}

                  export TF_CLI_ARGS="-no-color"

                  terraform init -backend-config=$bc

                  TF_OUT=\$(terraform plan \
                    -var-file=$tv \
                    -var 'ami_id=$ami_id' \
                    -var 'instance_type=${params.INSTANCE_CLASS}')

                  TF_ASG_CHECK=\$(echo "\$TF_OUT" | grep "aws_autoscaling_group.main.*(forces new resource)")
                  TF_LC_CHECK=\$(echo "\$TF_OUT" | grep "aws_launch_configuration.app.*(forces new resource)")
                  TF_AMI_CHECK=\$(echo "\$TF_OUT" | grep "image_id:.*(forces new resource)")
                  TF_HIGHCPU_CHECK=\$(echo "\$TF_OUT" | grep "aws_autoscaling_policy.high-cpu.*(new resource required)")
                  TF_LOWCPU_CHECK=\$(echo "\$TF_OUT" | grep "aws_autoscaling_policy.low-cpu.*(new resource required)")

                  TF_PLAN_CHECK=\$(echo "\$TF_OUT" | grep "Plan: 4 to add, ${params.TF_EXPECT_CHANGES} to change, 4 to destroy.")

                  if [ -z "\$TF_ASG_CHECK" ] || [ -z "\$TF_LC_CHECK" ] || [ -z "\$TF_AMI_CHECK" ] || [ -z "\$TF_HIGHCPU_CHECK" ] || [ -z "\$TF_LOWCPU_CHECK" ] || [ -z "\$TF_PLAN_CHECK" ]
                  then
                    echo "Terraform plan does not match expectations."
                    exit 1
                  fi
                """
              }
            }
          }
        }
      }
    }

    stage('Deploy AMI') {
      steps {
        script {
          dir('code') {
            withAwsCli(credentialsId: aws_creds, defaultRegion: 'us-east-1') {
              withCredentials([
                file(credentialsId: backend_config, variable: 'bc'),
                file(credentialsId: terraform_vars, variable: 'tv')
              ]) {
                sh """
                  cd terraform/${params.ENV}

                  export TF_CLI_ARGS="-no-color"

                  terraform init -backend-config=$bc
                  terraform apply \
                    -var-file=$tv \
                    -var 'ami_id=$ami_id' \
                    -var 'instance_type=${params.INSTANCE_CLASS}' \
                    -auto-approve
                """
              }
            }
          }
        }
      }
    }

    stage('Django - migrate and collectstatic') {
      steps {
        script {
          dir('code') {
            withAwsCli(credentialsId: aws_creds, defaultRegion: 'us-east-1') {
              withCredentials([
                file(credentialsId: private_key, variable: 'pk'),
                file(credentialsId: vault_pass, variable: 'vp')
              ]) {
                sh """
                  . venv/bin/activate

                  rm -Rf ./tmp

                  EC2_INI_PATH=inventory/config/${params.ENV}.ini \
                  ansible-playbook playbook/django/main.yml \
                    --vault-password-file ${vp} \
                    --private-key ${pk} \
                    -i inventory/ec2.py \
                    -l 'tag_Function_app_AppServer' \
                    -e 'env=${params.ENV}' \
                    -e 'collectstatic=${params.COLLECT_STATIC}' \
                    -e 'migrate=${params.MIGRATE}'
                """
              }
            }
          }
        }
      }
      when {
        expression {
          params.COLLECT_STATIC == "yes" || params.MIGRATE == "yes"
        }
      }
    }
  }

  post {
    success {
      withCredentials([
        string(credentialsId: 'hipchat-room', variable: 'room'),
        string(credentialsId: 'hipchat-server', variable: 'server'),
        string(credentialsId: 'hipchat-token', variable: 'token')
      ]) {
        hipchatSend(
            color: 'GREEN',
            notify: true,
            message: "SUCCESS: ${env.JOB_NAME} [${params.ENV}]",
            room: room,
            sendAs: '',
            server: server,
            token: token,
            v2enabled: true
        )
      }

      script {
        if (params.ENV == "prod" || params.ENV == "impl") {
          withAwsCli(credentialsId: aws_creds, defaultRegion: 'us-east-1') {
            release_version = sh(
              script: "aws ec2 describe-images --image-ids ${ami_id} --query 'Images[0].Tags[?Key==`Release`].Value' --output text",
              returnStdout: true
            ).trim()
          }

          withCredentials([
            string(credentialsId: "new-relic-app-id-${params.ENV}", variable: 'app_id'),
            string(credentialsId: 'new-relic-api-key', variable: 'api_key'),
          ]) {
            sh """
              python3 ./lib/mark_deployment.py \
                --app_id ${app_id} \
                --api_key ${api_key} \
                --version ${release_version}
            """
          }
        }
      }
    }

    failure {
      withCredentials([
        string(credentialsId: 'hipchat-room', variable: 'room'),
        string(credentialsId: 'hipchat-server', variable: 'server'),
        string(credentialsId: 'hipchat-token', variable: 'token')
      ]) {
        hipchatSend(
          color: 'RED',
          notify: true,
          message: "FAILED: ${env.JOB_NAME} [${params.ENV}]",
          room: room,
          sendAs: '',
          server: server,
          token: token,
          v2enabled: true
        )
      }
    }
  }
}
